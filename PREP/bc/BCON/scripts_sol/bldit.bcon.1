#! /bin/csh -f

# ===================== BCONv5.0.1 Build Script ===================== #
# Usage: bldit.bcon >&! bldit.bcon.log                                #
# Requirements: I/O API & netCDF libs, Git, and a Fortran compiler    #
# Note that this script is configured/tested for Red Hat Linux O/S    #
# The following environment variables must be set for this script to  #
# build an executable.                                                #
#   setenv M3MODEL <source code Git repository>                       #
#   setenv M3LIB   <code libraries>                                   #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/html/help.html                #
# =================================================================== #

 set BLD_OS = `uname -s`        ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host!"
    exit 1
 endif

#> Source the config.cmaq file to set the build environment
 source ../../../../config.cmaq

#> Check for M3MODEL and M3LIB settings (M3MODEL points to CCTM):
 if ( ! -e $M3MODEL || ! -e $M3LIB ) then 
    echo "   $M3MODEL or $M3LIB directory not found"
    exit 1
 endif
 echo "   CCTM repository path: $M3MODEL"
 echo "           library path: $M3LIB"

#> If $M3MODEL not set, default to $M3HOME
 if ( $?M3MODEL ) then
    echo "         Model repository path: $M3MODEL"
 else
    setenv M3MODEL $M3HOME
    echo " default Model repository path: $M3MODEL"
 endif

 set echo

#> Source Code Repository
 setenv REPOROOT $M3MODEL/CCTM/src     #> location of the CCTM source code
 set GlobInc = $REPOROOT/ICL           #> location of the global include files
 set Mechs   = $REPOROOT/MECHS         #> location of the chemistry mechanism defining files

#:#:#:#:#:#:#:#:#:#:#:# Begin User Input Section #:#:#:#:#:#:#:#:#:#:#:#

#> user choices: Git repository
 set Project = $M3HOME/BCON-git
 set GlobInc = $M3HOME/ICL-git
 set Mechs   = $M3MODEL/CCTM/MECHS
 echo "   Model repository path: $Project"
 setenv REPOROOT $Project

#> user choices: base working directory, application string
 set Base = $cwd
#set Base = ..
#set APPL  = v51
#set APPL  = v51_profile
 set APPL  = v52_profile
 set MODEL = BCON_${APPL}_$EXEC_ID
 set CFG   = cfg.$MODEL

#> The "BLD" directory for checking out and compiling source code
 set Bld = $Base/BLD_BCON_${APPL}
 if ( ! -e "$Bld" ) then
    mkdir $Bld
 else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
    endif
 endif
 
 cd $Bld

#> user choice: don't copy source files
 set CopySrc        # copy the source files into the BLD directory

#set Opt = verbose  # show requested commands as they are executed

#> user choice: make a Makefile
 set MakeFileOnly   # builds a Makefile to make the model, but does not compile -
                    # comment out to also compile the model (default if not set)

#> user choices: various modules

 set ModCommon = common

 set ModType   = profile
#set ModType   = m3conc
#set ModType   = tracer

#> user choices: mechanism
#set Mechanism = cb05tucl_ae6_aq/
#set Mechanism = cb05tump_ae6_aq/
 set Mechanism = cb05e51_ae6_aq/
#set Mechanism = cb05mp51_ae6_aq/
#set Mechanism = saprc07tb_ae6_aq/
#set Mechanism = saprc07tc_ae6_aq/
#set Mechanism = saprc07tic_ae6i_aq/
#set Mechanism = racm2_ae6_aq/
 set Tracer    = trac0               # default: no tracer species

#> Set full path of Fortran 90 compiler
 set FC = ${myFC}
 set FP = $FC

#> Set location of M3Bld executable
#set Blder = $M3LIB/build/bldmake
#set Blder = "$M3HOME/BLDMAKE-git/bldmake -verbose"
 set Blder = "$M3HOME/BLDMAKE/bldmake -verbose"

#> Set location of libraries/include files
 set IOAPI   = "${M3LIB}/ioapi_3.1/Linux2_${system}${compiler_ext} -lioapi"
 set IOAPIMOD = ${M3LIB}/ioapi_3.1/Linux2_${system}${compiler_ext}

 set NETCDF = "${M3LIB}/netcdf/lib -lnetcdf -lnetcdff"

#> Set compiler flags
 set F_FLAGS    = "${myFFLAGS} -I ${IOAPIMOD} -I."
 set F90_FLAGS  = "${myFRFLAGS} -I ${IOAPIMOD} -I."
 set CPP_FLAGS  = "-Dmech_ck_off"
#set CPP_FLAGS  = ""
 set C_FLAGS    = "${myCFLAGS} -DFLDMN"
 set LINK_FLAGS = "${myLINK_FLAG}"

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

 set LIB1 = "-L${IOAPI}"
 set LIB2 = "-L${NETCDF}"
 set LIBS = "$LIB1 $LIB2"

#source $Base/relinc.bcon
#if ( $status ) exit 1

 set ICL_MECH  = $Mechs/$Mechanism
 set ICL_TRAC  = $Mechs/$Tracer

#> make the config file

 set Cfile = ${CFG}.bld
 set quote = '"'

 echo                                                               > $Cfile
 echo "model       $MODEL;"                                        >> $Cfile
 echo                                                              >> $Cfile
 echo "FPP         $FP;"                                           >> $Cfile
 echo                                                              >> $Cfile
 set text = "$quote$CPP_FLAGS$quote;"
 echo "cpp_flags   $text"                                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f_compiler  $FC;"                                           >> $Cfile
 echo                                                              >> $Cfile
 echo "f_flags     $quote$F_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f90_flags   $quote$F90_FLAGS$quote;"                        >> $Cfile
 echo                                                              >> $Cfile
 echo "c_flags     $quote$C_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "link_flags  $quote$LINK_FLAGS$quote;"                       >> $Cfile
 echo                                                              >> $Cfile
 echo "libraries   $quote$LIBS$quote;"                             >> $Cfile
 echo                                                              >> $Cfile
 echo "global      $Opt;"                                          >> $Cfile
 echo                                                              >> $Cfile

 set text="// mechanism:"
 echo "$text ${Mechanism}"                                         >> $Cfile
 echo "// project repository: ${Project}"                          >> $Cfile
 echo                                                              >> $Cfile
 if ( $compiler == gfort ) then
   set ICL_CONST = '.'
   set ICL_MECH = '.'
 endif
#echo "include SUBST_RXCMMN     $ICL_MECH/RXCM.EXT;"               >> $Cfile
#echo "include SUBST_RXDATA     $ICL_MECH/RXDT.EXT;"               >> $Cfile
 echo                                                              >> $Cfile

 set text = "common"
 echo "// required" $text                                          >> $Cfile
 echo "Module ${ModCommon};"                                       >> $Cfile
 echo                                                              >> $Cfile

 set text = "profile, m3conc, tracer"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModType};"                                         >> $Cfile
 echo                                                              >> $Cfile

 if ( $?ModMisc ) then
    echo "Module ${ModMisc};"                                      >> $Cfile
    echo                                                           >> $Cfile
 endif

#> make the makefile or the model executable

 unalias mv rm
 if ( $?MakeFileOnly ) then
    if ( $?CopySrc ) then
       $Blder -makefo $Cfile
    else
       $Blder -makefo -git_local $Cfile   # $Cfile = ${CFG}.bld
     # totalview -a $Blder -makefo $Cfile
    endif
 else   # also compile the model
    if ( $?CopySrc ) then
       $Blder $Cfile
    else
       $Blder -git_local $Cfile
    endif
 endif
 mv Makefile $Bld/Makefile.$COMPILER
#if ( -e $Bld/Makefile.$COMPILER && -e $Bld/Makefile ) rm $Bld/Makefile
#ln -s $Bld/Makefile.$COMPILER $Bld/Makefile
 if ( -e Makefile.$COMPILER && -e Makefile ) rm Makefile
 ln -s Makefile.$COMPILER Makefile

 if ( $status != 0 ) then
    echo "   *** failure in $Blder ***"
    exit 1
 endif
 if ( -e "$Base/${CFG}" ) then
    echo "   >>> previous ${CFG} exists, re-naming to ${CFG}.old <<<"
    mv $Base/${CFG} $Base/${CFG}.old
 endif
 mv ${CFG}.bld $Bld/${CFG}

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#
 cd $Project
 set brnch = `git branch`
 unset echo
 @ i = 0
 while ( $i < $#brnch )
    @ i++
    if ( "$brnch[$i]" == "*" ) @ l = $i + 1
 end
 set rep = `echo $cwd | tr "/" "#"`
 set rln = "repo:${rep},branch:${brnch[$l]},compiler:${COMPILER}"
 set ref = $Bld/$rln
 /bin/touch $ref
 if ( -d $Project/branch ) /bin/cp $Project/branch/branch.* $Bld

 exit
